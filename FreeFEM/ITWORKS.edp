int CA=3, CK=4, CG=5, CB=6; 

// w2 disk radius, h disk thick, d2 separation in mm
real w2=4, h=0.26, d2=8.554, A=1.623, E=0.494;

border bottomA(t=-w2,w2){ x=t; y=d2; label=CA;};
border rightA(t=d2,d2+h){ x=w2; y=t; label=CA;};
border topA(t=w2,-w2){ x=t; y=d2+h; label=CA;};
border leftA(t=d2+h,d2){ x=-w2; y=t; label=CA;};

border bottomK(t=-w2,w2){ x=t; y=-h; label=CK;};
border rightK(t=-h,0){ x=w2; y=t; label=CK;};
border topK(t=w2,-w2){ x=t; y=0; label=CK;};
border leftK(t=0,-h){ x=-w2; y=t; label=CK;};

border bottomG(t=-4,-2){ x=t; y=4.26+A; label=CG;};
border rightG(t=4.26+A,4.26+A+E){ x=-2; y=t; label=CG;};
border topG(t=-2,-4){ x=t; y=4.26+A+E; label=CG;};
border leftG(t=4.26+A+E,4.26+A){ x=-4; y=t; label=CG;};

border enclosure(t=0,2*pi){x=10*cos(t); y=10*sin(t); label=CB;}

int n=15;
mesh Th = buildmesh(enclosure(3*n)+
             bottomA(-w2*n)+topA(-w2*n)+rightA(-h*n)+leftA(-h*n)+
             bottomK(-w2*n)+topK(-w2*n)+rightK(-h*n)+leftK(-h*n)+
             bottomG(4*n)+topG(4*n)+rightG((4.26+A+E)*n)+leftG((4.26+A+E)*n));

fespace Vh(Th,P1);

Vh u,v;
real u0=100;

problem Laplace(u,v,solver=LU) =
          int2d(Th)(dx(u)*dx(v) + dy(u)*dy(v))
        + on(CA,u=u0)+on(CK,u=0)+on(CG,u=0);

real error=0.01;
for (int i=0;i<1;i++){
   Laplace;
   Th=adaptmesh(Th,u,err=error);
   error=error/2.0;
}
Laplace;

Vh Ex, Ey;
Ex = -dx(u);
Ey = -dy(u);

plot(u,value=true);
